const Self = @This();

const std = @import("std");
const fs = std.fs;
const mem = std.mem;
const zon = std.zon;
const meta = std.meta;
const posix = std.posix;
const log = std.log.scoped(.config);

const wayland = @import("wayland");
const river = wayland.client.river;

const kwm = @import("kwm");

const rule = @import("config/rule.zig");

var allocator: mem.Allocator = undefined;

var path: []const u8 = undefined;
var config: ?Self = null;
var user_config: ?make_fields_optional(Self) = null;
const default_config: Self = @import("default_config");

pub const lock_mode = "lock";
pub const default_mode = "default";
pub const WindowRule = rule.Window;
pub const InputDeviceRule = rule.InputDevice;
pub const LibinputDeviceRule = rule.LibinputDevice;
pub const XkbKeyboardRule = rule.XkbKeyboard;


fn enum_struct(comptime E: type, comptime T: type) type {
    const info = @typeInfo(E);
    if (info != .@"enum") @panic("E is needed to be a enum");

    var fields: [info.@"enum".fields.len]std.builtin.Type.StructField = undefined;
    for (0.., info.@"enum".fields) |i, field| {
        fields[i] = std.builtin.Type.StructField {
            .name = field.name,
            .type = T,
            .is_comptime = false,
            .default_value_ptr = switch (@typeInfo(T)) {
                .optional => blk: {
                    const default_value: T = null;
                    break :blk &default_value;
                },
                else => null,
            },
            .alignment = @alignOf(T),
        };
    }

    const S = @Type(
        .{
            .@"struct" = .{
                .layout = .auto,
                .is_tuple = false,
                .fields = &fields,
                .decls = &.{},
            },
        }
    );

    const Getter = struct {
        pub const instance: @This() = .{};
        pub fn get(self: *const @This(), e: E) T {
            inline for (@typeInfo(E).@"enum".fields) |field| {
                if (@intFromEnum(e) == field.value) return @field(@as(*const S, @ptrCast(@alignCast(self))), field.name);
            }
            unreachable;
        }
    };

    return @Type(
        .{
            .@"struct" = .{
                .layout = .auto,
                .is_tuple = false,
                .fields = &([_]std.builtin.Type.StructField {.{
                    .name = "getter",
                    .type = Getter,
                    .default_value_ptr = &Getter.instance,
                    .is_comptime = false,
                    .alignment = @alignOf(T),
                }} ++ fields),
                .decls = &.{},
            },
        }
    );
}


fn make_optional(comptime T: type) type {
    return switch (@typeInfo(T)) {
        .optional => T,
        .@"struct" => @Type(.{ .optional = .{ .child = make_fields_optional(T) } }),
        else => @Type(.{ .optional = .{ .child = T } }),
    };
}


fn make_fields_optional(comptime T: type) type {
    const info = @typeInfo(T);
    if (info != .@"struct") @panic("T is needed to be a struct");

    var fields: [info.@"struct".fields.len]std.builtin.Type.StructField = undefined;
    for (0.., info.@"struct".fields) |i, field| {
        const new_T = make_optional(field.type);
        const default_value: new_T = null;
        fields[i] = std.builtin.Type.StructField {
            .name = field.name,
            .type = new_T,
            .default_value_ptr = &default_value,
            .is_comptime = false,
            .alignment = @alignOf(new_T),
        };
    }

    return @Type(
        .{
            .@"struct" = .{
                .layout = .auto,
                .is_tuple = false,
                .fields = &fields,
                .decls = &.{},
            },
        }
    );
}


fn field_mask(comptime T: type) type {
    const info = @typeInfo(T);
    if (info != .@"struct") @panic("T is needed to be a struct");

    var fields: [info.@"struct".fields.len]std.builtin.Type.StructField = undefined;
    for (0.., info.@"struct".fields) |i, field| {
        const default_value = false;
        fields[i] = std.builtin.Type.StructField {
            .name = field.name,
            .type = bool,
            .default_value_ptr = &default_value,
            .is_comptime = false,
            .alignment = @alignOf(bool),
        };
    }

    return @Type(
        .{
            .@"struct" = .{
                .layout = .auto,
                .is_tuple = false,
                .fields = &fields,
                .decls = &.{},
            },
        }
    );
}


fn merge(comptime T: type, base: *const T, new: *const make_optional(T)) T {
    if (new.* == null) return base.*;

    var result: T = undefined;
    const info = @typeInfo(T);
    switch (info) {
        .@"struct" => |struct_info| inline for (struct_info.fields) |field| {
            @field(result, field.name) = merge(field.type, &@field(base.*, field.name), &@field(new.*.?, field.name));
        },
        else => result = new.*.?,
    }
    return result;
}


pub fn deep_equal(comptime T: type, a: *const T, b: *const T) bool {
    return switch (@typeInfo(T)) {
        .@"struct" => |info| blk: {
            inline for (info.fields) |field| {
                if (!deep_equal(
                    field.type,
                    @ptrCast(&@field(a, field.name)),
                    @ptrCast(&@field(b, field.name)),
                )) {
                    break :blk false;
                }
            }
            break :blk true;
        },
        .array => |info| blk: {
            if (a.len != b.len) break :blk false;

            for (a.*, b.*) |elem_a, elem_b| {
                if (!deep_equal(info.child, &elem_a, &elem_b)) {
                    break :blk false;
                }
            }

            break :blk true;
        },
        .pointer => |info| switch (info.size) {
            .slice => blk: {
                if (a.len != b.len) break :blk false;

                for (a.*, b.*) |elem_a, elem_b| {
                    if (!deep_equal(info.child, &elem_a, &elem_b)) {
                        break :blk false;
                    }
                }

                break :blk true;
            },
            else => unreachable,
        },
        .@"union" => |info| blk: {
            if (info.tag_type != null) {
                const tag_a = meta.activeTag(a.*);
                const tag_b = meta.activeTag(b.*);

                if (tag_a != tag_b) break :blk false;

                inline for (info.fields) |field| {
                    if (@field(T, field.name) == tag_a) {
                        break :blk deep_equal(
                            field.type,
                            &@field(a.*, field.name),
                            &@field(b.*, field.name),
                        );
                    }
                }
                unreachable;
            } else unreachable;
        },
        .optional => |info|
            if (a.* == null and b.* == null) true
            else if (a.* == null or b.* == null) false
            else deep_equal(info.child, &a.*.?, &b.*.?),
        .float => @abs(a.*-b.*) < 1e-9,
        .int, .bool, .@"enum" => a.* == b.*,
        .void => true,
        else => unreachable,
    };
}


// copy from std.zon.parse
pub fn zon_free(gpa: mem.Allocator, value: anytype) void {
    const Value = @TypeOf(value);

    switch (@typeInfo(Value)) {
        .bool, .int, .float, .@"enum" => {},
        .pointer => |pointer| {
            switch (pointer.size) {
                .one => {
                    zon_free(gpa, value.*);
                    gpa.destroy(value);
                },
                .slice => {
                    // avoid free error
                    if (pointer.child == u8 and value.ptr == default_mode[0..].ptr) return;

                    for (value) |item| {
                        zon_free(gpa, item);
                    }
                    gpa.free(value);
                },
                .many, .c => comptime unreachable,
            }
        },
        .array => for (value) |item| {
            zon_free(gpa, item);
        },
        .@"struct" => |@"struct"| inline for (@"struct".fields) |field| {
            zon_free(gpa, @field(value, field.name));
        },
        .@"union" => |@"union"| if (@"union".tag_type == null) {
            if (comptime requiresAllocator(Value)) unreachable;
        } else switch (value) {
            inline else => |_, tag| {
                zon_free(gpa, @field(value, @tagName(tag)));
            },
        },
        .optional => if (value) |some| {
            zon_free(gpa, some);
        },
        .vector => |vector| for (0..vector.len) |i| zon_free(gpa, value[i]),
        .void => {},
        else => comptime unreachable,
    }
}

fn requiresAllocator(T: type) bool {
    return switch (@typeInfo(T)) {
        .pointer => true,
        .array => |array| return array.len > 0 and requiresAllocator(array.child),
        .@"struct" => |@"struct"| inline for (@"struct".fields) |field| {
            if (requiresAllocator(field.type)) {
                break true;
            }
        } else false,
        .@"union" => |@"union"| inline for (@"union".fields) |field| {
            if (requiresAllocator(field.type)) {
                break true;
            }
        } else false,
        .optional => |optional| requiresAllocator(optional.child),
        .vector => |vector| return vector.len > 0 and requiresAllocator(vector.child),
        else => false,
    };
}


env: []const struct { []const u8, []const u8 },

working_directory: union(enum) {
    none,
    home,
    custom: []const u8,
},

startup_cmds: []const []const []const u8,

xcursor_theme: ?struct {
    name: []const u8,
    size: u32,
},

background: ?u32,

bar: struct {
    show_default: bool,
    position: enum {
        top,
        bottom,
    },
    font: []const u8,
    color: struct {
        normal: struct {
            fg: u32,
            bg: u32,
        },
        select: struct {
            fg: u32,
            bg: u32,
        },
    },
    status: union(enum) {
        text: []const u8,
        stdin,
        fifo: []const u8,
    },
    click: enum_struct(
        kwm.BarArea,
        enum_struct(kwm.Button, ?kwm.BindingAction),
    ),
},

sloppy_focus: bool,

cursor_wrap: enum {
    none,
    on_output_changed,
    on_focus_changed,
},

remember_floating_geometry: bool,

auto_swallow: bool,

default_attach_mode: enum_struct(kwm.layout.Type, kwm.WindowAttachMode),

default_window_decoration: kwm.WindowDecoration,

border: struct {
    width: i32,
    color: struct {
        focus: u32,
        unfocus: u32,
    }
},

tags: []const []const u8,

layout: struct {
    default: kwm.layout.Type,
    tile: kwm.layout.tile,
    grid: kwm.layout.grid,
    monocle: kwm.layout.monocle,
    deck: kwm.layout.deck,
    scroller: kwm.layout.scroller,
},
layout_tag: struct {
    tile: enum_struct(kwm.layout.tile.MasterLocation, []const u8),
    grid: enum_struct(kwm.layout.grid.Direction, []const u8),
    monocle: []const u8,
    deck: enum_struct(kwm.layout.deck.MasterLocation, []const u8),
    scroller: []const u8,
    float: []const u8,
},

bindings: struct {
    repeat_info: kwm.KeyboardRepeatInfo,
    mode_tag: []const struct { []const u8, []const u8 },
    key: []const struct {
        mode: []const u8 = default_mode,
        keysym: []const u8,
        modifiers: river.SeatV1.Modifiers,
        event: kwm.XkbBindingEvent,
    },
    pointer: []const struct {
        mode: []const u8 = default_mode,
        button: kwm.Button,
        modifiers: river.SeatV1.Modifiers,
        event: kwm.PointerBindingEvent,
    }
},

window_rules: []const rule.Window,
input_device_rules: []const rule.InputDevice,
libinput_device_rules: []const rule.LibinputDevice,
xkb_keyboard_rules: []const rule.XkbKeyboard,


fn free_user_config() void {
    if (user_config) |cfg| {
        log.debug("free user config", .{});

        zon_free(allocator, cfg);
    }
}


pub fn init(al: *const mem.Allocator, config_path: []const u8) void {
    log.debug("config init", .{});

    allocator = al.*;
    path = config_path;

    user_config = try_load_user_config();
    refresh_config();
}


pub inline fn deinit() void {
    log.debug("config deinit", .{});

    free_user_config();
}


pub fn reload() field_mask(Self) {
    log.debug("reload user config", .{});

    if (try_load_user_config()) |cfg| {
        var mask: field_mask(Self) = .{};

        const info = @typeInfo(Self).@"struct";
        if (user_config) |old_cfg| {
            inline for (info.fields) |field| {
                if (!deep_equal(
                    @FieldType(@TypeOf(cfg), field.name),
                    &@field(old_cfg, field.name),
                    &@field(cfg, field.name),
                )) {
                    @field(mask, field.name) = true;
                }
            }
        } else {
            inline for (info.fields) |field| {
                @field(mask, field.name) = true;
            }
        }

        const modified = blk: {
            inline for (@typeInfo(@TypeOf(mask)).@"struct".fields) |field| {
                if (@field(mask, field.name)) {
                    break :blk true;
                }
            }
            break :blk false;
        };

        if (modified) {
            free_user_config();
            user_config = cfg;

            refresh_config();
        } else {
            zon_free(allocator, cfg);
        }

        return mask;
    } else return .{};
}


fn try_load_user_config() ?make_fields_optional(Self) {
    log.info("try load user config from `{s}`", .{ path });

    const file = fs.cwd().openFile(path, .{ .mode = .read_only }) catch |err| {
        switch (err) {
            error.FileNotFound => {
                log.warn("`{s}` not exists", .{ path });
            },
            else => {
                log.err("access file `{s}` failed: {}", .{ path, err });
            }
        }
        return null;
    };
    defer file.close();

    const stat = file.stat() catch |err| {
        log.err("stat file `{s}` failed: {}", .{ path, err });
        return null;
    };
    const buffer = allocator.alloc(u8, stat.size+1) catch |err| {
        log.err("alloc {} byte failed: {}", .{ stat.size+1, err });
        return null;
    };
    defer allocator.free(buffer);

    _ = file.readAll(buffer) catch return null;
    buffer[stat.size] = 0;

    @setEvalBranchQuota(15000);
    return zon.parse.fromSlice(
        make_fields_optional(Self),
        allocator,
        buffer[0..stat.size:0],
        null,
        .{.ignore_unknown_fields = true},
    ) catch |err| {
        log.err("load user config failed: {}", .{ err });
        return null;
    };
}


fn refresh_config() void {
    log.debug("refresh config", .{});

    if (user_config) |cfg| {
        config = undefined;
        inline for (@typeInfo(Self).@"struct".fields) |field| {
            @field(config.?, field.name) = merge(
                field.type,
                &@field(default_config, field.name),
                &@field(cfg, field.name),
            );
        }
        log.debug("config: {any}", .{ config.? });
    }
}


pub inline fn get() *Self {
    if (config == null) {
        config = default_config;
    }
    return &config.?;
}


pub fn get_mode_tag(self: *const Self, mode: []const u8) ?[]const u8 {
    for (self.bindings.mode_tag) |pair| {
        const m, const t = pair;
        if (mem.eql(u8, m, mode)) return t;
    }
    return null;
}
